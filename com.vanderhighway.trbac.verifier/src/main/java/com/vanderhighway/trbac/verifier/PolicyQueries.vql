/*******************************************************************************
 * Copyright (c) 2010-2015, Zoltan Ujhelyi, Gabor Szarnyas
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-v20.html.
 * 
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
package com.vanderhighway.trbac.verifier

import "https://vanderhighway.com/trbac/2020"

// Policy
pattern policy(policy: Policy) {
    Policy(policy);
}

// Policy
pattern session(session: Session) {
    Session(session);
}

// Example Constraint - All Role Names
pattern roleName(role: Role, name : java String) {
    Role.name(role,name);
}

// ----- Utility Patterns -----

pattern allJuniors(senior: Role, junior: Role) {
	Role.juniors(senior, junior);
} or {
	Role.juniors(senior, refjunior);
	find allJuniors(refjunior, junior);
}

pattern allJuniorsIncludingSelf(senior: Role, roleP: Role) {
	senior == roleP;
} or {
	find allJuniors(senior, roleP);
}

pattern allSubdemarcations(supdemarcation: Demarcation, subdemarcation: Demarcation) {
	Demarcation.subdemarcations(supdemarcation, subdemarcation);
} or {
	Demarcation.subdemarcations(supdemarcation, refsubdemarcation);
	find allSubdemarcations(refsubdemarcation, subdemarcation);
}

pattern allSubDemarcationsIncludingSelf(supdemarcation: Demarcation, demarcationP: Demarcation) {
	supdemarcation == demarcationP;
} or {
	find allSubdemarcations(supdemarcation, demarcationP);
}

// ----------------------------


// ----- Access Relation -----

pattern accessRelation(user: User, permission: Permission) {
    User.UR(user,role);
    Role.RD(role, demarcation);
   	Demarcation.DP(demarcation, permission);
}

pattern accessRelation2(user: User, permission: Permission) {
    User.UR(user,role);
    find allJuniorsIncludingSelf(role, roleP);
    Role.RD(roleP, demarcation);
    find allSubDemarcationsIncludingSelf(demarcation, demarcationP);
   	Demarcation.DP(demarcationP, permission);
}

// ---------------------------

// ----- Example Cardinality Constraints -----

pattern userShouldHaveARole(user: User) {
    neg User.UR(user, _);
}
pattern roleShouldHaveADemarcation(role: Role) {
    neg Role.RD(role, _);
}
pattern demarcationShouldHaveAPermission(dem: Demarcation) {
    neg Demarcation.DP(dem, _);
}

pattern onlyOneDirector(role:Role) {
	Role.name(role, "Director");
	n == count Role.RU(role, _);
	check(n >= 2);
}

pattern onlyOneRnDManager(role:Role) {
	Role.name(role, "RnD_Manager");
	n == count Role.RU(role, _);
	check(n >= 2);
}

pattern onlyOneOperationsManager(role:Role) {
	Role.name(role, "Operations_Manager");
	n == count Role.RU(role, _);
	check(n >= 2);
}

// ------------------------------------------


// ----- Example Static SoD constraints -----

pattern SoDEmployeeAndContractor(userE: User, userC: User, employee : Role, contractor : Role) {
    Role.name(employee, "Employee");
    Role.name(contractor, "Contractor");
   	Role.RU(employee, userE);
   	Role.RU(contractor, userC);
    userE==userC;
}

pattern SoDEmployeeAndVisitor(userE: User, userV: User, employee : Role, visitor : Role) {
    Role.name(employee, "Employee");
    Role.name(visitor, "Visitor");
   	Role.RU(employee, userE);
   	Role.RU(visitor, userV);
    userE==userV;
}

// ------------------------------------------


// ----- Example Prerequiste Constraints -----

pattern PrerequisiteEverybodyHasAccessToLobby(user: User) {
	Permission.name(pLobby,"Lobby");
    neg find accessRelation(user, pLobby);
}

pattern PrerequisiteVaultImpliesOpenOffice(user: User) {
	Permission.name(pVault,"Vault");
	find accessRelation(user, pVault);
	Permission.name(pOpenOffice,"OpenOffice");
    neg find accessRelation(user, pOpenOffice);
}

// ------------------------------------------


// ---- Constrains for Transformations ----
pattern userShouldHaveASession(user: User) {
    neg User.US(user, _);
}

pattern sessionShouldHaveAUser(session: User) {
    neg User.US(_,session);
}

pattern SessionOfUser(user: User, session: Session) {
    User.US(user, session);
}

//
//pattern InheritedDemarcation(role: Role, demarcation: Demarcation) {
//    Role.RDH(role, demarcation);
//}
//
//pattern MissingInheritedDemarcation(roleJunior: Role, roleSenior: Role, demarcation: Demarcation) {
//	find MissingDirectInheritedDemarcation(roleJunior, roleSenior, demarcation);
//	} or {
//	find MissingIndirectInheritedDemarcation(roleJunior, roleSenior, demarcation);
//}
//
//pattern MissingDirectInheritedDemarcation(roleJunior: Role, roleSenior: Role, demarcation: Demarcation) {
//	Role.juniors(roleSenior, roleJunior);
//	Role.RD(roleJunior,demarcation);
//	neg Role.RDH(roleSenior, demarcation);
//}
//
//pattern MissingIndirectInheritedDemarcation(roleJunior: Role, roleSenior: Role, demarcation: Demarcation) {
//	Role.juniors(roleSenior, roleJunior);
//	Role.RDH(roleJunior, demarcation);
//	neg Role.RDH(roleSenior,demarcation);
//}
// ----------------------------------------