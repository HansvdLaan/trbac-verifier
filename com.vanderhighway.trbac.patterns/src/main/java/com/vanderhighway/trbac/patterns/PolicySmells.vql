package com.vanderhighway.trbac.patterns

import "https://vanderhighway.com/trbac/2020"
import "http://www.eclipse.org/emf/2002/Ecore"
import java ^com.vanderhighway.trbac.aggregators.distinct
import java ^com.vanderhighway.trbac.aggregators.Scenario


pattern UnusedRole(role: Role) {
	Role.name(role,_); //The role should have a name!
	neg Role.RU(role, _);
	neg Role.seniors(role, _);
}

pattern UnusedDemarcation(demarcation: Demarcation) {
	Demarcation.name(demarcation,_); //The demarcation should have a name!
	neg TemporalGrantRule.demarcation(_, demarcation);
	neg Demarcation.superdemarcations(demarcation, _);
}

pattern UnusedPermission(permission: Permission) {
	Permission.name(permission,_); //The permission should have a name!
	neg Permission.PD(permission, _);
}

//TODO: user with no roles!
// TODO: zombie roles/permissies. 

pattern ZombieDemarcation(demarcation: Demarcation) {
	TemporalGrantRule.demarcation(_, demarcation);
	neg find RSD(_,_,demarcation);
	neg Demarcation.superdemarcations(demarcation, _);
} 

pattern ZombiePermission(permission: Permission) {	
	neg find UnusedPermission(permission);
	neg find RSP(_,_,permission);
}

pattern IgnoredRoleInheritance(user: User, roleJunior: Role, roleSenior: Role) {
	User.UR(user, roleSenior);
	Role.juniors+(roleSenior, roleJunior);
	User.UR(user, roleJunior);
}

pattern IgnoredDemarcationInheritance(role: Role, scenario: java Scenario, subDemarcation: Demarcation, supDemarcation: Demarcation) {
	find RSD(role, scenario, supDemarcation);
	Demarcation.subdemarcations+(supDemarcation, subDemarcation);
	find RSD(role, scenario, subDemarcation);
}

pattern GodUser(user: User) {
	currentSPCount == count find USP(user, _, _);
	permissionCount == count Permission(_);
	scenarioCount == count find Scenarios(_);
	maxSPCount == eval(permissionCount * scenarioCount);
	check(currentSPCount == maxSPCount); 
}

pattern GodRole(role: Role) {
	currentSPCount == count find RSP(role, _, _);
	permissionCount == count Permission(_);
	scenarioCount == count find Scenarios(_);
	maxSPCount == eval(permissionCount * scenarioCount);
	check(currentSPCount == maxSPCount); 
}

pattern UninvocablePermission(user: User, scenario: java Scenario, permission: Permission, zone: SecurityZone) {
	Permission.PO(permission, zone);
    find USP(user, scenario, permission);
    find SecurityZoneAccessAllowed(scenario, zone);
    neg find SecurityZoneAccessible(user, scenario, zone);
}

pattern UserCanGetTrapped(user: User, scenario: java Scenario, zone: SecurityZone) {
    find SecurityZoneAccessible(user, scenario, zone);
    neg find SecurityZoneLeavable(user, scenario, zone);
}


